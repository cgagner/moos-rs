use std::str::FromStr;
use crate::helpers::remove_first_last;
use crate::lexers::Location;
use crate::nsplug_lexer::{State, Token};
use crate::parser::{MacroDefinition, MacroCondition, MacroType, Line};
use crate::error::MoosParseError;
use lalrpop_util::ErrorRecovery;

//grammar<'input>(input: &'input str);
grammar<'s, 'input>(state: &'s mut State<'input>, input: &'input str);

pub Lines: Vec<Line<'input>> = <Line*>;

pub Line: Line<'input> = {
    MacroDefine => <>,
    MacroInclude => <>,
    //MacroElse => <>,
    //MacroEndif => <>,
    "EOL" => Line::EndOfLine,
    <comment:"Comment"> => Line::Comment(comment),
    UnknownMacro => <>,
    <variable:"PlugVariable"> => Line::PlugVariable(variable),
    <variable:"PlugUpperVariable"> => Line::PlugUpperVariable(variable),
    ! => { state.errors.push(<>); Line::Error },
}

// TODO: 
//   1. Add MacroIfDef
//   2. Add MacroElseIfDef
//   3. Add MacroIfNotDef


MacroDefine: Line<'input> = {
    <d:"#define"> <def:MacroDefinition> <comment:"Comment"?> "EOL" => Line::Macro(MacroType::Define(def), comment),
}

MacroDefinition: MacroDefinition<'input> = {
    <name:"ValueString"> " " <value:Value?> => {
        MacroDefinition::new(name, value)
    }
}

MacroElse: Line<'input> = {
    <d:"#else"> <l:@L> <extras:Value*> <r:@R> <comment:"Comment"?> "EOL" => {
        if !extras.is_empty() {
            let e = lalrpop_util::ErrorRecovery { 
                error: lalrpop_util::ParseError::User {
                    // TODO: Need to create a more descriptive error
                    error: MoosParseError::new_missing_new_line(l, Location{line: r.line, index: r.index+1})
                },
                dropped_tokens: vec![]
            };
            state.errors.push(e);
            Line::Error
        } else {
            Line::Macro(MacroType::Else, comment)
        }
    },
}

MacroEndif: Line<'input> = {
    <d:"#endif"> <l:@L> <extras:Value*> <r:@R> <comment:"Comment"?> "EOL" => {
        if !extras.is_empty() {
            let e = lalrpop_util::ErrorRecovery { 
                error: lalrpop_util::ParseError::User {
                    // TODO: Need to create a more descriptive error
                    error: MoosParseError::new_missing_new_line(l, Location{line: r.line, index: r.index+1})
                },
                dropped_tokens: vec![]
            };
            state.errors.push(e);
            Line::Error
        } else {
            Line::Macro(MacroType::EndIf, comment)
        }
    },
}

MacroInclude: Line<'input> = {
    <d:"#include"> <path:"ValueString"> <l:@L> <extras:Value*> <r:@R> <comment:"Comment"?> "EOL" => {
        if !extras.is_empty() {
            let e = lalrpop_util::ErrorRecovery { 
                error: lalrpop_util::ParseError::User {
                    // TODO: Need to create a more descriptive error
                    error: MoosParseError::new_missing_new_line(l, Location{line: r.line, index: r.index+1})
                },
                dropped_tokens: vec![]
            };
            state.errors.push(e);
            Line::Error
        } else {
            Line::Macro(MacroType::Include(path), comment)
        }
    }, 
    <d:"#include"> <path:"Quote"> <l:@L> <extras:Value*> <r:@R> <comment:"Comment"?> "EOL" => {
        if !extras.is_empty() {
            let e = lalrpop_util::ErrorRecovery { 
                error: lalrpop_util::ParseError::User {
                    // TODO: Need to create a more descriptive error
                    error: MoosParseError::new_missing_new_line(l, Location{line: r.line, index: r.index+1})
                },
                dropped_tokens: vec![]
            };
            state.errors.push(e);
            Line::Error
        } else {
            Line::Macro(MacroType::Include(path), comment)
        }
    }, 
}

UnknownMacro: Line<'input> = {
     <l:@L> <m:"#unknown"> => {
        let e = lalrpop_util::ErrorRecovery { 
                error: lalrpop_util::ParseError::User {
                    error: MoosParseError::new_unknown_macro(l, m)
                },
                dropped_tokens: vec![]
            };
        state.errors.push(e);
        Line::Error
    }
}

Value: crate::parser::Value<'input> = {
    <value:"bool"> => crate::parser::Value::Boolean(value.0, value.1),
    <value:"int"> => crate::parser::Value::Integer(value.0, value.1),
    <value:"float"> => crate::parser::Value::Float(value.0, value.1),
    <value:"ValueString"> => crate::parser::Value::String(value),
    <value:"Quote"> => crate::parser::Value::String(value),
    <value:"PlugVariable"> => crate::parser::Value::PlugVariable(value),
    <value:"PlugUpperVariable"> => crate::parser::Value::PlugUpperVariable(value),
    <value:"PartialPlugVariable"> => crate::parser::Value::PartialPlugVariable(value),
    <value:"PartialPlugUpperVariable"> => crate::parser::Value::PartialPlugUpperVariable(value),
}

// ---------------------------------------------------------------------------
// Token Definitions 
// ---------------------------------------------------------------------------

extern {  
    type Location = crate::lexers::Location;
    type Error = MoosParseError<'input>;
    
    enum Token<'input> {
        "Comment" => Token::Comment(<&'input str>),
        "Quote" => Token::Quote(<&'input str>),
        "PartialQuote" => Token::PartialQuote(<&'input str>, <char>),
        "ValueString" => Token::ValueString(<&'input str>),
        "EOL" => Token::EOL,
        "PlugVariable" => Token::PlugVariable(<&'input str>),
        "PartialPlugVariable" => Token::PartialPlugVariable(<&'input str>),
        "PlugUpperVariable" => Token::PlugUpperVariable(<&'input str>),
        "PartialPlugUpperVariable" => Token::PartialPlugUpperVariable(<&'input str>),
        "int" => Token::Integer(<i64>, <&'input str>),
        "float" => Token::Float(<f64>, <&'input str>),
        "bool" => Token::Boolean(<bool>, <&'input str>),
        "#define" => Token::MacroDefine,
        "#else" => Token::MacroElse,
        "#elseifdef" => Token::MacroElseIfDef,
        "#endif" => Token::MacroEndIf,
        "#ifdef" => Token::MacroIfDef,
        "#ifndef" => Token::MacroIfNotDef,
        "#include" => Token::MacroInclude,
        "#unknown" => Token::UnknownMacro(<&'input str>),
        "||" => Token::OrOperator,
        "&&" => Token::AndOperator,
        " " => Token::Space,
    }
}
